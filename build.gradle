import com.amazonaws.auth.policy.Policy
import com.amazonaws.auth.policy.Principal
import com.amazonaws.auth.policy.Statement
import com.amazonaws.auth.policy.actions.S3Actions
import com.amazonaws.auth.policy.resources.S3BucketResource
import com.amazonaws.auth.policy.resources.S3ObjectResource
import com.amazonaws.services.s3.model.ObjectMetadata

import jp.classmethod.aws.gradle.s3.SyncTask

// Build repositories
buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}

        maven {
            name = "sponge"
            url = "http://repo.spongepowered.org/maven/"
        }
		

	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT"
        classpath 'org.spongepowered:mixingradle:0.4-SNAPSHOT'
		classpath "jp.classmethod.aws:gradle-aws-plugin:0.+"
	}

}

// Set up FE required repositories
repositories {
	maven {
		name = "sk89q"
		url = "http://maven.sk89q.com/repo/"
	}
	maven {
		name = "sponge"
		url = "http://repo.spongepowered.org/maven/"
	}
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
//we have to do this for jitpack
apply plugin: 'maven'
apply plugin: "jp.classmethod.aws.s3"

aws {
	profileName = "minecraft"
	region = "us-east-1"
}

def myBucketName = 'broad-participation'

/************************************************************
 * General configuration
 */

// Get buildNumber from environment variable
ext.buildNumber = "0"
if (System.getenv("BUILD_NUMBER") != null) {
	buildNumber = System.getenv("BUILD_NUMBER")
}

// Set build properties
ext.baseVersion = "1.8.5"
version = baseVersion + "." + buildNumber
group = 'com.github.CityOfLearning'
archivesBaseName = "forgeessentials"

sourceCompatibility = 1.8
targetCompatibility = 1.8

// Set up forge options
minecraft {
	version = "1.8.9-11.15.1.1902-1.8.9"
	runDir = "rundir"
	
	mappings = "stable_22"
	makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.


	replaceIn "src/utils/java/com/forgeessentials/commons/BuildInfo.java"
	replace "_VERSION_", project.version
}

// Configure shared manifest
ext.sharedManifest = manifest {
	attributes (
		"BuildID": baseVersion,
		"BuildNumber": buildNumber,
		"FMLCorePluginContainsFMLMod": "true",
		"ForceLoadAsMod": "true",
        "MixinCompatibilityLevel": "JAVA_8",
	)
}

ext.serverManifest = manifest {
	from sharedManifest
	attributes (
		"TweakClass": "com.forgeessentials.core.preloader.FELaunchHandler",
		"TweakOrder": "0",
        "MixinConfigs": "mixins.forgeessentials.json",
		"FMLAT": "forgeessentials_at.cfg",
	)
}

/************************************************************
 * Dependency configuration
 */

// Create dependency configurations
configurations {
	includeMod
	shade
	shadeClient
	serverLib
	buildDep
	compile.extendsFrom buildDep, shade, shadeClient, serverLib
}

// Configure dependencies (build-only, shaded and packed libraries)
dependencies {
	// WorldEdit
    //buildDep group: "com.sk89q.worldedit", name: "worldedit-forge-1.8.9", version: "6.1.1-SNAPSHOT", changing: true
    buildDep "com.sk89q.worldedit:worldedit-core:6.1.1-SNAPSHOT"
    buildDep "com.sk89q.worldedit:worldedit-forge-mc1.8.9:6.1.1:dev"
	includeMod files('lib/worldedit-forge-mc1.8.9-6.1.1-SNAPSHOT-dist.jar')

	//buildDep "org.spongepowered:spongeforge:3.1.0-BETA-1046:deobf"
	buildDep 'org.spongepowered:spongeapi:4.1.0-SNAPSHOT'

	// Shade Mixin library
	shade (group: "org.spongepowered", name: "mixin", version: "0.5.6-SNAPSHOT", changing: true) {
		exclude group: 'org.ow2.asm', module: 'asm-tree'
	}
	shadeClient (group: "org.spongepowered", name: "mixin", version: "0.5.6-SNAPSHOT", changing: true) {
		exclude group: 'org.ow2.asm', module: 'asm-tree'
	}
}

/************************************************************
 * Configure source sets (e.g., src/main)
 */

sourceSets {
    def deps = [
		configurations.forgeGradleMcDeps,
		configurations.forgeGradleMc,
		configurations.provided
	]
	utils {
		compileClasspath = files(configurations.compile, sourceSets.api.output)
		compileClasspath += files deps
	}
	main {
		compileClasspath = files(configurations.compile, sourceSets.api.output, sourceSets.utils.output)
		compileClasspath += files deps
	}
	client {
		compileClasspath = files(configurations.compile, sourceSets.api.output, sourceSets.utils.output)
		compileClasspath += files deps
	}
}

// Configure server resource processing
project.processResources {
	from (sourceSets.main.resources.srcDirs) {
		include "mcmod.info"
		expand (
			"version": project.version, 
			"mcversion": project.minecraft.version,
		)
	}
}

// Configure client resource processing
project.processClientResources {
	from (sourceSets.client.resources.srcDirs) {
		include "mcmod.info"
		expand (
			"version": project.version, 
			"mcversion": project.minecraft.version,
		)
	}
}

/************************************************************
 * Configure server jar (default)
 */

// Pack libraries as zip file to include them in the jar file
task serverLibArchive(type: Zip) {
	archiveName = "libraries.zip"
	destinationDir = jar.destinationDir

	from (configurations.serverLib.copyRecursive()) { into("ForgeEssentials/lib") }
	from (configurations.includeMod) { into("mods") }
}

jar.doLast {
	delete (serverLibArchive.archivePath)
}

def serverRefMap = "${tasks.compileJava.temporaryDir}" + File.separator + "mixins.forgeessentials.refmap.json"
def clientRefMap = "${tasks.compileClientJava.temporaryDir}" + File.separator + "mixins.forgeessentials.client.refmap.json"

jar.doFirst {

	FileCollection collection = files { sourceSets.main.output }
	
	collection.collect { relativePath(it) }.sort().each { println it }
	
	collection.each {File file ->
		if (file.path.contains("classes")){
			FileCollection subColl = files { file.listFiles() }
			subColl.collect { relativePath(it) }.sort().each { if(it.contains("mcmod.info")){delete it} }
		}
	}
}

// Configure server jar (default)
jar {
	setDuplicatesStrategy(DuplicatesStrategy.EXCLUDE)
	dependsOn "serverLibArchive"
	classifier = "server"
	appendix = minecraft.version
	
	from sourceSets.main.output
	from sourceSets.utils.output
	from fileTree(serverLibArchive.archivePath)
	configurations.shade.copyRecursive().setTransitive(false).each { artifact ->
		from (zipTree(artifact))
	}
    from serverRefMap

	manifest {
		from serverManifest
	}
}

/************************************************************
 * Configure client jar
 */

task clientJar (dependsOn: "clientClasses", type: Jar) {
	classifier = "client"
	appendix = minecraft.version
	
	from sourceSets.client.output
	from sourceSets.utils.output
    from clientRefMap
	configurations.shadeClient.copyRecursive().setTransitive(false).each { artifact ->
		from (zipTree(artifact))
	}
	
	manifest {
		from sharedManifest
		attributes (
			"MixinConfigs": "mixins.forgeessentials.client.json",
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
		)
	}
}

mixin {
    add sourceSets.main, "mixins.forgeessentials.refmap.json"
    add sourceSets.client, "mixins.forgeessentials.client.refmap.json"
}

/************************************************************
 * Configure reobfuscation
 *

def mixinSrg = "${tasks.reobf.temporaryDir}" + File.separator + "mixins.srg"
def mixinClientSrg = "${tasks.reobf.temporaryDir}" + File.separator + "mixinsClient.srg"
afterEvaluate {
	tasks.compileJava.options.compilerArgs += ["-AreobfSrgFile=${tasks.reobf.srg}", "-AoutSrgFile=${mixinSrg}", "-AoutRefMapFile=${serverRefMap}"]
	tasks.compileClientJava.options.compilerArgs += ["-AreobfSrgFile=${tasks.reobf.srg}", "-AoutSrgFile=${mixinClientSrg}", "-AoutRefMapFile=${clientRefMap}"]
}
*/

reobf {
    clientJar {
        classpath = sourceSets.main.compileClasspath
    }
}

/************************************************************
 * Others
 */

// Add default artifacts for task "build"
artifacts {
	//archives deobfJar
	archives clientJar
}

task wrapper(type: Wrapper) {
	gradleVersion = "2.10"
}

reobfJar.doLast {
    ant.checksum file: tasks.jar.archivePath
}

reobfClientJar.doLast {
    ant.checksum file: tasks.clientJar.archivePath
}

//
build.doLast {
	File serverjarFile = file(tasks.jar.archivePath)
	File servermd5File = file('./build/libs/'+archivesBaseName +'-' + minecraft.version +'-' +version+'-server.jar.MD5')
	File clientjarFile = file(tasks.clientJar.archivePath)
	File clientmd5File = file('./build/libs/'+archivesBaseName +'-' + minecraft.version +'-' +version+'-client.jar.MD5')
	serverjarFile.renameTo(file('./build/libs/' + archivesBaseName + '-server.jar'))
	servermd5File.renameTo(file('./build/libs/' + archivesBaseName + '-server.MD5'))
	clientjarFile.renameTo(file('./build/libs/' + archivesBaseName + '-client.jar'))
	clientmd5File.renameTo(file('./build/libs/' + archivesBaseName + '-client.MD5'))
}

task publish(type: SyncTask, dependsOn: build) {
	source file('./build/libs/') //must be a directory
	bucketName myBucketName
	
	metadataProvider { bucket, key, file ->
	
		ObjectMetadata m = new ObjectMetadata()
		m.setCacheControl("no-cache, no-store")
		return m
	}
	
	setAcl 'PublicRead'
}